buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2' // 0.12.+ 버전은 안드로이드 스튜디오 0.8.+ 버전부터
        // JaKe Wharton 것을 쓰다가 jcandksolutions 가 문제를 해결해서 여기의 것을 사용
//        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.+'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.2.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 42
        versionName "2.4.0"
        applicationId "com.yooiistudios.morningkit"
    }

    signingConfigs {
        release {
            storeFile file("/Development/Android/keystore/morningkit.jks")
            keyAlias "morningkit_key"

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword "tempPassword"
            keyPassword "tempPassword"
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    sourceSets {
//        instrumentTest.setRoot('src/test')
        androidTest.setRoot('src/test')
    }

    packagingOptions { // gralde 0.7.1 + Studio 0.4.0 에서 생긴 duplicate 버그를 해결하기 위함
        pickFirst 'META-INF/services/javax.annotation.processing.Processor'
        pickFirst 'META-INF/DEPENDENCIES.txt'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LGPL2.1'
    }

    // java 1.7을 지원하는 코드. 하지만 OS단에서 java 1.6을 사용하고 있음
//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_7
//        targetCompatibility JavaVersion.VERSION_1_7
//    }
}

apply plugin: 'android-unit-test'

dependencies {
    // 내가 만든 모듈
    compile project(':libraries:SKAlarmManager')
    compile project(':libraries:SKAlarmSound')
    compile project(':libraries:view-pager-indicator-library') // MNSettingActivityProject 에 필요
    compile project(':libraries:DatePickerLibrary')
    compile project(':libraries:TimePickerLibrary')
    compile project(':libraries:sAXRSSReader')

    // 안드로이드 기본
    compile 'com.android.support:support-v4:20.0.0' // 21.+ 버전부터 안드로이드 L 이라서 막아둠
    compile 'com.android.support:appcompat-v7:20.0.0'

    // 위치 서비스 관련
    compile 'com.google.android.gms:play-services:5.0.89' // 4.0.+ 에서 버전업

    // 모든 jar 파일 로딩
    compile fileTree(dir: 'libs', include: '*.jar')

    // 필요한 오픈소스 라이브러리들
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.jakewharton:butterknife:4.0.1'

    // Getter & Setter를 annotation으로 해결
    // (Android Studio용 플러그인용 jar 파일을 웹에서 받아서 <설정 -> plugin -> disk 에서 설치> 필요)
    compile 'org.projectlombok:lombok:1.12.2'

    // 설정앱에서 사용된 라이브러리
    compile 'com.nineoldandroids:library:2.4.0'

    // 토스트를 다양하게 사용 가능
    compile 'com.github.johnpersano:supertoasts:1.3.4@aar'

    // IOUtils - bitmap 저장시 활용
    compile 'commons-io:commons-io:2.4'

    // 시간 관련 많이 쓰이는 오픈소스
    compile 'joda-time:joda-time:2.3'

    //////////
    // TEST //
    //////////
//    testCompile 'junit:junit:4.10'
//    testCompile 'org.robolectric:robolectric:2.4-SNAPSHOT' // ActionBarActivity 의 문제를 해결하고자 2.2에서 변경
//    testCompile 'com.squareup:fest-android:1.0.+'
//
//    androidTestCompile 'junit:junit:4.10'
//    androidTestCompile 'org.robolectric:robolectric:2.4-SNAPSHOT'
//    androidTestCompile 'com.squareup:fest-android:1.0.+'
}

// 소스를 UTF-8로 컴파일, ../gradlew clean check 로 테스트할 경우 한글주석관련 warning을 없애기 위함
// 되도록이면 Android Studio의 encoding도 전부 UTF-8로 설정해주자
// https://code.google.com/p/android/issues/detail?id=55750
tasks.withType(Compile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

// release시 비밀번호를 직접 입력하기 위한 코드
task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    if (System.console() != null) {
        def storePw = new String(System.console().readPassword("Keystore password: "))
        def keyPw = new String(System.console().readPassword("Key password: "))

        android.signingConfigs.release.storePassword = storePw
        android.signingConfigs.release.keyPassword = keyPw
    }
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        // 커맨드에서 직접 빌드를 만들 경우에만 사용하자
        theTask.dependsOn "askForPasswords"
    }
}